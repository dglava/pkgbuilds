pkgname=labwc-noxwayland-undecorate-git
pkgver=0.9.0.r0.g4beee38
pkgrel=1
pkgdesc="Stacking Wayland compositor - no Xwayland, auto undecorate maximized windows"
arch=('x86_64')
url="https://github.com/johanmalm/labwc"
license=('GPL-2.0-only')
depends=('wlroots0.19' 'cairo' 'pango' 'libxml2' 'glib2' 'libinput'
         'libxkbcommon' 'wayland' 'scdoc' 'libpng' 'librsvg' 'libsfdo')
makedepends=('meson' 'git' 'wayland-protocols')
conflicts=('labwc' 'labwc-noxwayland' 'labwc-noxwayland-git' 'labwc-noxwayland-undecorate')
source=('git+https://github.com/labwc/labwc.git'
        'undecorate-0.9.0.patch')
sha1sums=('SKIP'
          '71d9d5b359cab753eca962e404b137fc15046a57')

pkgver() {
  cd ${pkgname%-noxwayland-undecorate-git}
  git describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname%-noxwayland-undecorate-git}
  # maximized windows get automatically undecorated, saving some
  # vertical space and looking nicer, since the panel displays the
  # window names anyway
  patch -p1 < "$srcdir/undecorate-0.9.0.patch"
}

build() {
  cd "${srcdir}"/"${pkgname%-noxwayland-undecorate-git}"
  arch-meson -Dxwayland=disabled build
  meson compile -C build
}

package() {
  cd "${srcdir}"/"${pkgname%-noxwayland-undecorate-git}"
  meson install -C build --destdir "${pkgdir}"
}

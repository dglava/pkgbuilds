pkgname=nomacs-git
pkgver=3.2.0.r211.ga37517d6
pkgrel=1
pkgdesc="A QT image viewer"
arch=("x86_64")
url="http://www.nomacs.org/"
license=('GPL3')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv' 'desktop-file-utils')
makedepends=("cmake" "qt5-tools")
source=("git+https://github.com/nomacs/nomacs.git")
md5sums=('SKIP')

pkgver() {
    cd "${pkgname%-git}"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${pkgname%-git}/ImageLounge"
    mkdir "build"
    cd "build"
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_PLUGINS=OFF
    make
}

package() {
    cd "${pkgname%-git}/ImageLounge/build"
    make DESTDIR="$pkgdir/" install
}

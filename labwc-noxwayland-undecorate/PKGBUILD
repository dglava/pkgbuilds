pkgname=labwc-noxwayland-undecorate
pkgver=0.9.0
pkgrel=1
pkgdesc="Stacking Wayland compositor - no Xwayland, auto undecorate maximized windows"
arch=('x86_64')
url="https://github.com/johanmalm/labwc"
license=('GPL-2.0-only')
depends=('wlroots0.19' 'cairo' 'pango' 'libxml2' 'glib2' 'libinput'
         'libxkbcommon' 'wayland' 'scdoc' 'libpng' 'librsvg' 'libsfdo')
makedepends=('meson' 'git' 'wayland-protocols')
conflicts=('labwc' 'labwc-noxwayland' 'labwc-noxwayland-git' 'labwc-noxwayland-undecorate-git')
source=("https://github.com/labwc/labwc/archive/refs/tags/${pkgver}.tar.gz"
        "undecorate-${pkgver}.patch")
sha1sums=('2c76ef196f0135fdcc85aac2d531edb71333b3b6'
          '71d9d5b359cab753eca962e404b137fc15046a57')

prepare() {
  cd ${srcdir}/${pkgname%-noxwayland-undecorate}-${pkgver}
  # maximized windows get automatically undecorated, saving some
  # vertical space and looking nicer, since the panel displays the
  # window names anyway
  patch -p1 < $srcdir/"undecorate-${pkgver}.patch"
}

build() {
  cd ${srcdir}/${pkgname%-noxwayland-undecorate}-${pkgver}
  arch-meson -Dxwayland=disabled build
  meson compile -C build
}

package() {
  cd ${srcdir}/${pkgname%-noxwayland-undecorate}-${pkgver}
  meson install -C build --destdir "${pkgdir}"
}

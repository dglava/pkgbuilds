pkgname=zukitre-mod-gtk-theme-git
pkgver=r294.115c867
pkgrel=1
pkgdesc="Zukitre GTK theme with slight modifications"
arch=('any')
url="https://github.com/dglava/pkgbuilds/tree/master/zukitre-mod-gtk-theme-git"
license=('GPL3')
makedepends=('git' 'meson')
source=('git+https://github.com/lassekongo83/zuki-themes.git'
        'colors.patch')
md5sums=('SKIP'
         '0e365cbfff3447743b0acca0702f1c0e')

prepare() {
  cd "$srcdir/zuki-themes"
  # dark checkboxes/radiobuttons
  patch -p1 < $srcdir/colors.patch
  cd "gtk/src/Zukitre/gtk-2.0/"
  rm "assets/"*
  bash "render-assets.sh"
}

pkgver() {
  cd "$srcdir/zuki-themes"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/zuki-themes"
  mkdir build
  arch-meson build
}

package() {
  cd "$srcdir/zuki-themes"
  DESTDIR="$pkgdir" ninja -C build install
  # rename theme
  mv "${pkgdir}/usr/share/themes/Zukitre" "${pkgdir}/usr/share/themes/Zukitre-mod"
  # remove unneeded things
  rm -rf "${pkgdir}/usr/share/themes/Zuki-shell"
  rm -rf "${pkgdir}/usr/share/themes/Zukitwo"
  rm -rf "${pkgdir}/usr/share/themes/Zukitwo-dark"
  rm -rf "${pkgdir}/usr/share/themes/Zukitre/xfwm4"
  rm -rf "${pkgdir}/usr/share/themes/Zukitre-dark"
}

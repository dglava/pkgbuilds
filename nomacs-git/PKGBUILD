pkgname=nomacs-git
pkgver=3.2.0.r2336.gb4af068a
pkgrel=1
pkgdesc="A QT image viewer"
arch=("x86_64")
url="http://www.nomacs.org/"
license=('GPL-3.0-only')
conflicts=('nomacs')
depends=('qt6-base' 'qt6-imageformats' 'qt6-svg' 'exiv2'
         'libraw' 'opencv' 'desktop-file-utils')
makedepends=("cmake" "qt6-tools" "git")
source=("git+https://github.com/nomacs/nomacs.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}/ImageLounge"
  mkdir "build"
  cd "build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_PLUGINS=OFF \
           -DENABLE_QUAZIP=OFF \
           -DENABLE_TESTING=OFF \
           -DENABLE_TIFF=OFF \
           -DENABLE_RAW=OFF \
           -DQT_VERSION_MAJOR=6
  make
}

package() {
  cd "${pkgname%-git}/ImageLounge/build"
  make DESTDIR="$pkgdir/" install
}

pkgname=labwc-noxwayland-git
pkgver=r1820.1b8e404
pkgrel=1
pkgdesc="Stacking Wayland compositor - without Xwayland, latest master branch"
arch=('x86_64')
url="https://github.com/johanmalm/labwc"
license=('GPL2')
depends=('wlroots0.16' 'cairo' 'pango' 'libxml2' 'glib2' 'libinput'
				 'libxkbcommon' 'wayland' 'scdoc')
makedepends=('meson' 'git' 'wayland-protocols')
conflicts=('labwc' 'labwc-noxwayland')
source=('git+https://github.com/labwc/labwc.git')
sha1sums=('SKIP')

pkgver() {
	cd ${pkgname%-noxwayland-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}"/"${pkgname%-noxwayland-git}"
  export PKG_CONFIG_PATH='/usr/lib/wlroots0.16/pkgconfig'
  arch-meson -Dxwayland=disabled build
  meson compile -C build
}

package() {
	cd "${srcdir}"/"${pkgname%-noxwayland-git}"
  meson install -C build --destdir "${pkgdir}"
}

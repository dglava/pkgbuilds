pkgname=zukitre-mod-gtk-theme-git
pkgver=r320.3267bd7
pkgrel=1
pkgdesc="Zukitre GTK theme with slight modifications"
arch=('any')
url="https://github.com/dglava/pkgbuilds/tree/master/zukitre-mod-gtk-theme-git"
license=('GPL3')
makedepends=('git' 'meson' 'sassc')
source=('git+https://github.com/lassekongo83/zuki-themes.git'
        'colors.patch')
md5sums=('SKIP'
         '6f6d710d6e3b8b35dea53a168e1b26bd')

prepare() {
  cd "$srcdir/zuki-themes"
  # dark checkboxes/radiobuttons; slightly altered base colors
  patch -p1 < $srcdir/colors.patch
  cd "gtk/src/Zukitre/gtk-2.0/"
  rm "assets/"*
  bash "render-assets.sh"
}

pkgver() {
  cd "$srcdir/zuki-themes"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/zuki-themes"
  mkdir build
  arch-meson build
}

package() {
  cd "$srcdir/zuki-themes"
  DESTDIR="$pkgdir" ninja -C build install
  # remove unneeded things
  rm -rf "${pkgdir}/usr/share/themes/Zuki-shell"
  rm -rf "${pkgdir}/usr/share/themes/Zukitwo"
  rm -rf "${pkgdir}/usr/share/themes/Zukitwo-dark"
  rm -rf "${pkgdir}/usr/share/themes/Zukitre/xfwm4"
  rm -rf "${pkgdir}/usr/share/themes/Zukitre-dark"
  # rename theme
  mv "${pkgdir}/usr/share/themes/Zukitre" "${pkgdir}/usr/share/themes/Zukitre-mod"
}
